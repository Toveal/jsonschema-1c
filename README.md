# JSON Schema для 1С

Надстройка над крейтом [jsonschema](https://crates.io/crates/jsonschema) для валидации JSON в 1С:Предприятие.

[![Draft 2020-12](https://img.shields.io/endpoint?url=https%3A%2F%2Fbowtie.report%2Fbadges%2Frust-jsonschema%2Fcompliance%2Fdraft2020-12.json)](https://bowtie.report/#/implementations/rust-jsonschema)
[![Draft 2019-09](https://img.shields.io/endpoint?url=https%3A%2F%2Fbowtie.report%2Fbadges%2Frust-jsonschema%2Fcompliance%2Fdraft2019-09.json)](https://bowtie.report/#/implementations/rust-jsonschema)
[![Draft 7](https://img.shields.io/endpoint?url=https%3A%2F%2Fbowtie.report%2Fbadges%2Frust-jsonschema%2Fcompliance%2Fdraft7.json)](https://bowtie.report/#/implementations/rust-jsonschema)
[![Draft 6](https://img.shields.io/endpoint?url=https%3A%2F%2Fbowtie.report%2Fbadges%2Frust-jsonschema%2Fcompliance%2Fdraft6.json)](https://bowtie.report/#/implementations/rust-jsonschema)
[![Draft 4](https://img.shields.io/endpoint?url=https%3A%2F%2Fbowtie.report%2Fbadges%2Frust-jsonschema%2Fcompliance%2Fdraft4.json)](https://bowtie.report/#/implementations/rust-jsonschema)

Методы, которые могут вызвать исключение, помечены знаком ⚠️. Описание ошибки можно получить через метод [ПолучитьОшибку](#получитьошибку-getlasterror).

---

- [Свойства](#свойства)
  - [Схема](#схема-schema)
  - [Формат](#формат-format)
  - [ИспользоватьДопФорматы](#использоватьдопформаты-usecustomformats)
  - [ИгнорироватьНеизвестныеФорматы](#игнорироватьнеизвестныеформаты-ignoreunknownformats)
  - [ПроверятьФорматы](#проверятьформаты-checkformats)
  - [Стандарт](#стандарт-draft)
  - [Версия](#версия-version)
- [Методы](#методы)
  - [УстановитьОсновнуюСхему ⚠️](#установитьосновнуюсхему-setmainscheme)
  - [ОчиститьОсновнуюСхему](#очиститьосновнуюсхему-clearmainscheme)
  - [Действителен ⚠️](#действителен-isvalid)
  - [Проверить ⚠️](#проверить-validate)
  - [ПолучитьОшибкиВалидации](#получитьошибкивалидации-getvalidationerror)
  - [ДобавитьСхему ⚠️](#добавитьсхему-addscheme)
  - [ЕстьСхема ⚠️](#естьсхема-hasscheme)
  - [ПолучитьСхемы](#получитьсхемы-getschemes)
  - [УдалитьСхему ⚠️](#удалитьсхему-deletescheme)
  - [УдалитьВсеСхемы](#удалитьвсесхемы-deleteallschemes)
  - [ПолучитьОшибку](#получитьошибку-getlasterror)
- [Пример использования](#пример-использования)

---

## Свойства

### Схема (Schema)

|             |                                                                 |
|-------------|-----------------------------------------------------------------|
| **Тип**     | Строка                                                          |
| **Доступ**  | Только чтение                                                   |
| **Описание**| Содержит основную схему, используемую для валидации JSON        |

---

### Формат (Format)

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Тип**             | Строка                                                       |
| **Доступ**          | Чтение и запись                                              |
| **По умолчанию**    | Не задан (выводится стандартное сообщение об ошибке)         |
| **Описание**        | Шаблон форматирования ошибок валидации для метода `Проверить`|

**Доступные плейсхолдеры:**

| Плейсхолдер     | Описание                                              |
|-----------------|-------------------------------------------------------|
| `{error}`       | Причина, по которой значение не прошло валидацию      |
| `{path}`        | JSON-путь к невалидному значению                      |
| `{instance}`    | Само значение, не прошедшее проверку                  |
| `{schema_path}` | Путь к правилу схемы, которое не выполнилось          |

**Пример:**

```bsl
Компонента.Формат = "Ошибка: {error} в {path}";
// Результат: "Ошибка: "123" does not match "^[0-9]{4}$" в /code"
```

---

### ИспользоватьДопФорматы (UseCustomFormats)

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Тип**             | Булево                                                       |
| **Доступ**          | Чтение и запись                                              |
| **По умолчанию**    | `Истина`                                                     |
| **Описание**        | Включает дополнительные форматы для ключевого слова `format` |

**Доступные форматы:**

| Формат               | Описание                                                                   |
|----------------------|----------------------------------------------------------------------------|
| `ru-inn-individual`  | ИНН физлица РФ с проверкой контрольной суммы                               |
| `ru-inn-legal-entity`| ИНН юрлица РФ с проверкой контрольной суммы                                |
| `kz-iin`             | ИИН физлица Казахстана с проверкой контрольной суммы                       |
| `local-date-time`    | Локальная дата 1С (без смещения часового пояса)                            |

> **Примечание:** Стандартный формат `date-time` требует смещения часового пояса (`2026-01-29T11:11:24+05:00`) или суффикса UTC (`2026-01-29T06:11:24Z`). 1С по умолчанию сериализует дату без смещения (`2026-01-29T11:12:09`) - для таких дат используйте local-date-time.

---

### ИгнорироватьНеизвестныеФорматы (IgnoreUnknownFormats)

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Тип**             | Булево                                                       |
| **Доступ**          | Чтение и запись                                              |
| **По умолчанию**    | `Истина`                                                     |
| **Описание**        | Определяет поведение при встрече неизвестного формата        |

При значении `Ложь` метод `УстановитьОсновнуюСхему` выбросит исключение, если в схеме указан несуществующий формат.

---

### ПроверятьФорматы (CheckFormats)

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Тип**             | Булево                                                       |
| **Доступ**          | Чтение и запись                                              |
| **По умолчанию**    | `Истина`                                                     |
| **Описание**        | Указывает, учитывать ли поле `format` при валидации          |

При значении `Ложь` форматы игнорируются - любая строка считается валидной независимо от указанного формата.

---

### Стандарт (Draft)

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Тип**             | Строка                                                       |
| **Доступ**          | Чтение и запись                                              |
| **По умолчанию**    | Не задан (автоопределение по схеме)                          |
| **Описание**        | Версия стандарта JSON Schema для валидации                   |

**Допустимые значения для записи:**

| Значение   | Стандарт       |
|------------|----------------|
| `"4"`      | Draft 4        |
| `"6"`      | Draft 6        |
| `"7"`      | Draft 7        |
| `"2019-09"`| Draft 2019-09  |
| `"2020-12"`| Draft 2020-12  |

**Значения при чтении:** `Draft4`, `Draft6`, `Draft7`, `Draft201909`, `Draft202012`

**Пример:**

```bsl
Компонента.Стандарт = "2020-12";
```

---

### Версия (Version)

|             |                                                                 |
|-------------|-----------------------------------------------------------------|
| **Тип**     | Строка                                                          |
| **Доступ**  | Только чтение                                                   |
| **Описание**| Версия компоненты                                               |

---

## Методы

### УстановитьОсновнуюСхему (SetMainScheme)

⚠️ Может вызвать исключение

|                     |                                   |
|---------------------|-----------------------------------|
| **Синтаксис**       | `УстановитьОсновнуюСхему(<Схема>)` |
| **Возврат**         | -                                |

**Параметры:**

| Имя    | Тип    | Описание   |
|--------|--------|------------|
| Схема  | Строка | JSON Schema|

**Описание:**

Компилирует и устанавливает основную схему для валидации. Методы `Действителен` и `Проверить` используют эту схему.

При компиляции учитываются текущие значения свойств `ИспользоватьДопФорматы`, `ИгнорироватьНеизвестныеФорматы`, `ПроверятьФорматы` и `Стандарт`. Изменение этих свойств после компиляции не влияет на уже установленную схему - требуется повторный вызов метода.

**Исключения:**

- Аргумент не является валидным JSON
- Не удалось скомпилировать схему
- Неизвестный формат (при `ИгнорироватьНеизвестныеФорматы = Ложь`)

---

### ОчиститьОсновнуюСхему (ClearMainScheme)

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Синтаксис**       | `ОчиститьОсновнуюСхему()`                                    |
| **Возврат**         | -                                                            |

**Описание:**

Очищает установленную основную схему. После вызова свойство `Схема` возвращает пустое значение.

---

### Действителен (IsValid)

⚠️ Может вызвать исключение

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Синтаксис**       | `Действителен(<JSON>)`                                       |
| **Возврат**         | Булево                                                       |

**Параметры:**

| Имя  | Тип    | Описание         |
|------|--------|------------------|
| JSON | Строка | JSON для проверки|

**Описание:**

Проверяет JSON на соответствие схеме. Возвращает `Истина` если валиден, `Ложь` если нет. Не предоставляет информацию об ошибках - для этого используйте метод `Проверить`.

**Исключения:**

- Аргумент не является валидным JSON
- Не установлена основная схема

---

### Проверить (Validate)

⚠️ Может вызвать исключение

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Синтаксис**       | `Проверить(<JSON>, <БуферОшибок>)`                           |
| **Возврат**         | Булево                                                       |

**Параметры:**

| Имя          | Тип    | Описание                                      |
|--------------|--------|-----------------------------------------------|
| JSON         | Строка | JSON для проверки                             |
| БуферОшибок  | Строка | Выходной параметр для записи ошибок валидации |

**Описание:**

Проверяет JSON на соответствие схеме. В `БуферОшибок` записывается JSON-массив строк с описанием ошибок. Формат каждой ошибки определяется свойством `Формат`.

Результат последней проверки также доступен через метод `ПолучитьОшибкиВалидации`.

**Исключения:**

- Аргумент не является валидным JSON
- Не установлена основная схема

---

### ПолучитьОшибкиВалидации (GetValidationError)

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Синтаксис**       | `ПолучитьОшибкиВалидации()`                                  |
| **Возврат**         | Строка / Неопределено                                        |

**Описание:**

Возвращает JSON-массив ошибок последнего вызова метода `Проверить`. Если метод `Проверить` ещё не вызывался - возвращает `Неопределено`.

---

### ДобавитьСхему (AddScheme)

⚠️ Может вызвать исключение

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Синтаксис**       | `ДобавитьСхему(<Схема>)`                                     |
| **Возврат**         | -                                                            |

**Параметры:**

| Имя   | Тип    | Описание                    |
|-------|--------|-----------------------------|
| Схема | Строка | JSON Schema с ключом `$id`  |

**Описание:**

Добавляет дополнительную схему для разрешения ссылок `$ref` в основной схеме. Схема должна содержать ключ `$id` с валидным URI.

**Исключения:**

- Аргумент не является валидным JSON
- В схеме отсутствует ключ `$id`
- Значение `$id` не является строкой
- Значение `$id` не является валидным [URI](https://datatracker.ietf.org/doc/html/rfc3986)

---

### ЕстьСхема (HasScheme)

⚠️ Может вызвать исключение

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Синтаксис**       | `ЕстьСхема(<URI>)`                                           |
| **Возврат**         | Булево                                                       |

**Параметры:**

| Имя | Тип    | Описание                    |
|-----|--------|-----------------------------|
| URI | Строка | Идентификатор схемы (`$id`) |

**Описание:**

Проверяет, существует ли дополнительная схема с указанным URI в коллекции. Возвращает `Истина` если схема найдена, `Ложь` если нет.

**Исключения:**

- Аргумент не является валидным [URI](https://datatracker.ietf.org/doc/html/rfc3986)

---

### ПолучитьСхемы (GetSchemes)

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Синтаксис**       | `ПолучитьСхемы()`                                            |
| **Возврат**         | Строка                                                       |

**Описание:**

Возвращает JSON-объект со всеми дополнительными схемами. Ключи - URI схем (`$id`), значения - сами схемы.

**Пример результата:**

```json
{
  "https://example.com/person": {"type": "object", ...},
  "https://example.com/address": {"type": "object", ...}
}
```

---

### УдалитьСхему (DeleteScheme)

⚠️ Может вызвать исключение

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Синтаксис**       | `УдалитьСхему(<URI>)`                                        |
| **Возврат**         | -                                                            |

**Параметры:**

| Имя | Тип    | Описание                    |
|-----|--------|-----------------------------|
| URI | Строка | Идентификатор схемы (`$id`) |

**Описание:**

Удаляет дополнительную схему из коллекции по её URI. Если схема с указанным URI не найдена, ничего не происходит.

**Исключения:**

- Аргумент не является валидным [URI](https://datatracker.ietf.org/doc/html/rfc3986)

---

### УдалитьВсеСхемы (DeleteAllSchemes)

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Синтаксис**       | `УдалитьВсеСхемы()`                                          |
| **Возврат**         | -                                                            |

**Описание:**

Очищает коллекцию всех дополнительных схем.

---

### ПолучитьОшибку (GetLastError)

|                     |                                                              |
|---------------------|--------------------------------------------------------------|
| **Синтаксис**       | `ПолучитьОшибку()`                                           |
| **Возврат**         | Строка / Неопределено                                        |

**Описание:**

Возвращает описание последней произошедшей ошибки. Если ошибок не было - возвращает `Неопределено`.

Вызывайте сразу после исключения - каждый успешный вызов метода (кроме `ПолучитьОшибку`) сбрасывает ошибку.

---

## Пример использования

```1c
Процедура ПроверитьТелоЗапроса()

    КомпонентаПодключена = ПодключитьВнешнююКомпоненту(ПутьККомпоненте(), "JsonСхема",
        ТипВнешнейКомпоненты.Native, ТипПодключенияВнешнейКомпоненты.НеИзолированно);

    Если Не КомпонентаПодключена Тогда
        Возврат;
    КонецЕсли;

    Компонента = Новый("AddIn.JsonСхема.JsonSchema1C");

    // Добавление дополнительной схемы для $ref
    Попытка
        Компонента.ДобавитьСхему(ДополнительнаяСхема());
    Исключение
        Сообщить(Компонента.ПолучитьОшибку());
    КонецПопытки;

    // Настройка
    Компонента.ИспользоватьДопФорматы = Ложь;
    Компонента.Стандарт = "2020-12";
    Компонента.Формат = "Ошибка: {error} в {path}";

    // Установка основной схемы
    Попытка
        Компонента.УстановитьОсновнуюСхему(ОсновнаяСхема());
    Исключение
        Сообщить(Компонента.ПолучитьОшибку());
        Возврат;
    КонецПопытки;

    // Быстрая проверка
    Попытка
        Сообщить("Валиден: " + Компонента.Действителен(ТестовыйJSON()));
    Исключение
        Сообщить(Компонента.ПолучитьОшибку());
    КонецПопытки;

    // Подробная проверка с получением ошибок
    БуферОшибок = "";
    Попытка
        Компонента.Проверить(ТестовыйJSON(), БуферОшибок);
        Сообщить(БуферОшибок);
    Исключение
        Сообщить(Компонента.ПолучитьОшибку());
    КонецПопытки;

    // Повторное получение ошибок валидации
    Сообщить(Компонента.ПолучитьОшибкиВалидации());

КонецПроцедуры

Функция ОсновнаяСхема()
    Возврат "{
    |  ""type"": ""object"",
    |  ""properties"": {
    |    ""person"": { ""$ref"": ""https://example.com/person"" }
    |  },
    |  ""required"": [""person""]
    |}";
КонецФункции

Функция ДополнительнаяСхема()
    Возврат "{
    |  ""$id"": ""https://example.com/person"",
    |  ""type"": ""object"",
    |  ""properties"": {
    |    ""name"": { ""type"": ""string"" },
    |    ""age"": { ""type"": ""integer"", ""minimum"": 0 }
    |  },
    |  ""required"": [""name"", ""age""]
    |}";
КонецФункции

Функция ТестовыйJSON()
    Возврат "{ ""person"": { ""name"": 123, ""age"": 30 } }";
КонецФункции

Функция ПутьККомпоненте()
    Возврат "ПутьДоКомпоненты";
КонецФункции
```

**Вывод:**

```
Валиден: Нет
["Ошибка: 123 is not of type \"string\" в /person/name"]
["Ошибка: 123 is not of type \"string\" в /person/name"]
```
